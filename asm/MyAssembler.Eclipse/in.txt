; VARIABLES OF MAIN
Y0:	.DB	0
Y1:	.DB	0

; MAIN
MAIN:
	MOV	A, RTP0A
	STA	RDKEYIP
	JMP	RDKEY
RTP0A:	MOV	A, R3
	STA	Y0
	MOV	A, RTP1A
	STA	RDKEYIP
	JMP	RDKEY
RTP1A:	LDA	Y0
	MOV	R2, A
	MOV	A, RTP0B
	STA	TOHEXIP
	JMP	TOHEX
RTP0B:	MOV	A, R3
	STA	Y0

	MOV	A, RTP2A
	STA	RDKEYIP
	JMP	RDKEY
RTP2A:	MOV	A, R3
	STA	Y1
	MOV	A, RTP3A
	STA	RDKEYIP
	JMP	RDKEY
RTP3A:	LDA	Y1
	MOV	R2, A
	MOV	A, RTP1B
	STA	TOHEXIP
	JMP	TOHEX
RTP1B:	MOV	A, R3
	STA	Y1

	MOV	R2, 0
	LDA	Y1
	MOV	R1, A
	MOV	R3, A
	LDA	Y0
	MOV	R0, A
NEXTA:	MOV	A, R0
	DEC	A
	JZ	DONEA
	MOV	R0, A
	MOV	A, R3
	ADD	A, R1
	MOV	R3, A
	JC	CARRYA
	JMP	NEXTA
CARRYA:	MOV	A, R2
	INC	A
	MOV	R2, A
	JMP	NEXTA

DONEA:	MOV	A, RTP0C
	STA	DIVHRDIP
	JMP	DIVHRD
RTP0C:	MOV	A, R2
	STA	Y1
	MOV	A, RTP1C
	STA	TODECIP
	JMP	TODEC
RTP1C:	MOV	A, R3
	STA	Y0
	LDA	Y1
	MOV	R3, A
	MOV	A, RTP2C
	STA	TODECIP
	JMP	TODEC
RTP2C:	MOV	A, R3
	STA	Y1
	HALT


; DIVIDE 2-BYTE HEX BY 100
; PARAMS:
; D0	R3	; I
; D1	R2	; I
; REM	R3	; O
; QUT	R2	; O

; LOCAL VARIABLES:
QUTB:	.DB	0	; L
AB:	.DB	0	; L

DIVHRD:	MOV	R1, 0
	MOV	R0, 100
	MOV	A, 0
	STA	QUTB
NEXTB:	MOV	A, R3
	SUB	A, R0
	JC	RSM1B
	STA	AB
	MOV	A, R2
	MOV	R0, 1
	SUB	A, R0
	JC	RSM0B
	LDA	QUTB
	MOV	R2, A
	JMP	(000)
DIVHRDIP:
	.DB	0

RSM0B:	MOV	R2, A
	MOV	R0, 100
	LDA	AB
RSM1B:	MOV	R3, A
	LDA	QUTB
	INC	A
	STA	QUTB
	JMP	NEXTB


; CONVERT 1 HEX VALUE TO 2 BCD DIGIT
; PARAMS:
; VAL	R3	; I, O

TODEC:	MOV	R2, 0
	MOV	R1, 10
NEXTC:	MOV	A, R3
	SUB	A, R1
	JC	AGAINC
	MOV	A, R2
	RRC	A
	RRC	A
	RRC	A
	RRC	A
	RRC	A
	ADD	A, R3
	MOV	R3, A
	JMP	(000)
TODECIP:
	.DB	0
AGAINC:	MOV	R3, A
	MOV	A, R2
	INC	A
	MOV	R2, A
	JMP	NEXTC


; CONVERT 2 BCD DIGIT TO 1 HEX VALUE
; PRARMS:
; D0	R3	; I, O
; D1	R2	; I

TOHEX:	MOV	R0, 10
NEXTD:	MOV	A, R3
	ADD	A, R2
	MOV	R3, A
	MOV	A, R0
	DEC	A
	JZ	DONED
	MOV	R0, A
	JMP	NEXTD
DONED:	JMP	(000)
TOHEXIP:
	.DB	0


; READ A KEY
; PARAMS:
; KEY	R3	; O
RDKEY:	LDA	0X404
	JA0	RDKEY
	LDA	0X401
	MOV	R3, A
	JMP	(000)
RDKEYIP:
	.DB	0
